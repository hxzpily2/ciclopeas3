<?xml version="1.0" encoding="utf-8"?>
<!---
<b>Availability:</b> CICLOPE AS3 Classes - www.ciclope.art.br<br>
<b>License:</b> GNU LGPL version 3<br><br>
WindowCommunityOpen creates a window for community opening.
@author Lucas Junqueira - lucas@ciclope.art.br
-->
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   width="400" title="{resourceManager.getString('language', 'CW_OPENWINDOW')}" creationComplete="init()">
			   
	<fx:Declarations>
		<mx:ArrayCollection id="availableData"></mx:ArrayCollection>
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
		
		// FLASH PACKAGES
		import mx.managers.PopUpManager;
		import mx.collections.ArrayCollection;
		import mx.core.FlexGlobals;
		
		// CICLOPE CLASSES
		import art.ciclope.managanaeditor.*;
		
		/**
		 * Component initialize.
		 */
		private function init():void {
			this.closeButton.visible = false;
			// get data
			this.availableData = new ArrayCollection();
			WindowServerCheck.createServer("getcommunities", null, onData);
		}
		
		/**
		 * Data about available communities is ready.
		 */
		private function onData(server:ServerInteraction):void {
			if (server.error == ServerEvent.SERVER_ERROR) {
				Warning.createWarning(resourceManager.getString('language', 'CW_OPENERTITLE'), resourceManager.getString('error', server.errorID));
				PopUpManager.removePopUp(this);
			} else if (server.error == ServerEvent.SERVER_OK) {
				for (var index:uint = 0; index < server.data.child("community").length(); index++) {
					this.availableData.addItem({ label: String(server.data.community[index]), data: String(server.data.community[index].@id)});
				}
				this.availableComm.selectedIndex = 0;
			}
			server.kill();
		}
		
		/**
		 * Close this window.
		 */
		private function onCancel():void {
			PopUpManager.removePopUp(this);
		}
		
		/**
		 * Try to open the selected community.
		 */
		private function onOpen():void {
			if (this.availableComm.selectedIndex >= 0) {
				WindowServerCheck.createServer("communitydata", ("id=" + this.availableComm.selectedItem.data), onCommunity);
			}
		}
		
		/**
		 * Retrieve information about the selected community.
		 */
		public function onCommunity(server:ServerInteraction):void {
			if (server.error == ServerEvent.SERVER_ERROR) {
				Warning.createWarning(resourceManager.getString('language', 'CW_OPENERTITLE'), resourceManager.getString('error', server.errorID));
			} else if (server.error == ServerEvent.SERVER_OK) {
				LoadedData.community.clear();
				LoadedData.community.getData(server.data);
				LoadedData.stream.clear();
				FlexGlobals.topLevelApplication.processCommand("showStatus", (resourceManager.getString('language', 'CW_OPENOK') + " " + LoadedData.community.title));
				FlexGlobals.topLevelApplication.showCommunity();
			}
			server.kill();
			PopUpManager.removePopUp(this);
		}
		
		]]>
	</fx:Script>
	
	<s:VGroup x="0" y="0" width="100%" height="100%" styleName="paddingGroup">
		<s:Label text="{resourceManager.getString('language', 'CW_OPENLABEL')}"/>
		<s:List id="availableComm" dataProvider="{availableData}" width="100%" height="200"></s:List>
		<s:HGroup width="100%" horizontalAlign="right" verticalAlign="middle">
			<s:Button label="{resourceManager.getString('language', 'CW_OPENCANCEL')}" click="onCancel()" />
			<s:Button label="{resourceManager.getString('language', 'CW_OPENBUTTON')}" click="onOpen()" />
		</s:HGroup>
	</s:VGroup>
</s:TitleWindow>